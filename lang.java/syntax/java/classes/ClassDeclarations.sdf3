module java/classes/ClassDeclarations

// 8.1. Class Declarations

imports
  java/lexical/Identifiers
  java/types/ReferenceTypes
  java/classes/FieldDeclarations
  java/classes/MethodDeclarations
  java/classes/InstanceInitializers
  java/classes/StaticInitializers
  java/classes/EnumDeclarations
  java/classes/ConstructorDeclarations
  java/interfaces/Annotations
  java/interfaces/InterfaceDeclarations

context-free syntax
  
  ClassDeclaration.CD2NCD = NormalClassDeclaration
  ClassDeclaration.CD2ED  = EnumDeclaration
  
  NormalClassDeclaration.ClassDeclaration = <
  <{ClassModifier " "}*> class <Id><TypeParameters?> <SuperClass?> <SuperInterfaces?> {
    <{ClassBodyDeclaration "\n"}*>
  }>
  
  ClassModifier = Annotation
  ClassModifier.Public    = "public"
  ClassModifier.Protected = "protected"
  ClassModifier.Private   = "private"
  ClassModifier.Abstract  = "abstract"
  ClassModifier.Static    = "static"
  ClassModifier.Final     = "final"
  ClassModifier.Strictfp  = "strictfp"
  
  TypeParameters.TypeParameters               = [<[{TypeParameter ","}+]>]
  TypeParameter.TypeParameter                 = <<{Annotation " "}*> <Id>>
  TypeParameter.TypeParameterExtendsClassType = <<{Annotation " "}*> <Id> extends <{ClassType "& "}+>>
  
  SuperClass.SuperClass = <extends <ClassType>>
  
  SuperInterfaces.SuperInterface = <implements <{ClassType ", "}+>>
  
  ClassBodyDeclaration.CBD2CMD = ClassMemberDeclaration
  ClassBodyDeclaration.CBD2II  = InstanceInitializer
  ClassBodyDeclaration.CBD2SI  = StaticInitializer
  ClassBodyDeclaration.CBD2CD  = ConstructorDeclaration
  
  ClassMemberDeclaration.CMD2FD = FieldDeclaration
  ClassMemberDeclaration.CMD2MD = MethodDeclaration
  ClassMemberDeclaration.CMD2CD = ClassDeclaration
  ClassMemberDeclaration.CMD2ID = InterfaceDeclaration
  ClassMemberDeclaration.SemiColon = ";"
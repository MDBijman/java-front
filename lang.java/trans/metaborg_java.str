module metaborg_java

imports

  completion/completion
  pp
  outline
  java/-

rules // Debugging

  debug-show-aterm:
    (node, _, _, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path
    ; result   := node

  time-pretty-printing:
    (node, _, _, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path
    ; result   := <profile(!"pretty printing time: ", pp-metaborg-java-string)> node

rules

  editor-strip-skeleton:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      ext      := <get-extension> path
    ; filename := <guarantee-extension(|$[skeleton.[ext]])> path
    ; stripped := <strip-to-skeleton> ast
    ; result   := <pp-metaborg-java-string> stripped

  editor-strip-interface:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      ext      := <get-extension> path
    ; filename := <guarantee-extension(|$[interface.[ext]])> path
    ; stripped := <strip-to-public-interface> ast
    ; result   := <pp-metaborg-java-string> stripped

  editor-explicate:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path
    ; result := <java-explicate-injections;strip-annos> ast

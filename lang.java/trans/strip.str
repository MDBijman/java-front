module strip

imports

  signatures/java/-
  signatures/java/classes/-
  signatures/java/interfaces/-
  signatures/java/packages/-

  pp

rules

  editor-strip:
    (_, _, ast, path, project-path) -> (filename, result)
    with
      ext      := <get-extension> path
    ; filename := <guarantee-extension(|$[stripped.[ext]])> path
    ; stripped := <strip-all> ast
    ; result   := <pp-metaborg-java-string> stripped

  null-statement-list = ![<null-statement>]
  null-statement =
    !Throw( NewInstance(
              None()
            , []
            , Id("java")
            , [QualifiedId([], Id("lang")), QualifiedId([], Id("RuntimeException"))]
            , None()
            , [StringLiteral("\"Implementation stripped\"")]
            )
          )

  null-expression-list = ![<null-expression>]
  null-expression = !Null()

rules

  strip-all =
    strip-accesses
  ; strip-bodies
  ; strip-unused-imports

  is-nonpublic = not(getfirst(?Public()))
  is-private   = getfirst(?Private())
  is-protected = getfirst(?Protected())

rules

  strip-accesses = topdown(try(strip-access))

  // 7.3. Compilation Units
  strip-access = CompilationUnit(id, id, filter(not(is-access)))

  // 8.1. Class Declarations
  strip-body = ClassDeclaration(id, id, id, id, id, filter(not(is-access)))

rules

  // 8.1. Class Declarations
  is-access = ?ClassDeclaration(<is-nonpublic>, _, _, _, _, _)

  // 8.3. Field Declarations
  is-access = ?FieldDecl(<is-nonpublic>, _, _)

  // 8.4. Method Declarations
  is-access = ?MethodDecl(<is-nonpublic>, _, _)

  // 8.7. Static Initializers
  is-access = ?StaticInit(_)

  // 8.8. Constructor Declarations
//is-access = ?ConstrDecl(<has-mod>, _, _, _, _, _, _)

  // 9.1. Interface Declarations
  is-access = ?NormalInterface(<is-nonpublic>, _, _, _, _)

rules

  strip-bodies = topdown(try(strip-body))

  // 8.1. Class Declarations
  strip-body = ClassDeclaration(strip-anns, id, id, id, id, id)

  // 8.3. Field Declarations
  strip-body = FieldDecl(strip-anns, id, id)
  strip-body = ?VariableDeclInit(<id>, _)

  // 8.4. Method Declarations
  strip-body = MethodDecl(strip-anns, id, id)

  // 8.8. Constructor Declarations
  strip-body = ConstrDecl(id, id, id, id, id, id, ![])

  // 8.9. Enum Types
  strip-body = EnumConstArgs(null-expression-list)

  // 9.1. Interface Declarations
  strip-body = NormalInterface(strip-anns, id, id, id, id)

  // 14.2. Blocks
  strip-body = Block(null-statement-list)

  strip-anns = filter(not(?Anno(_, _) + ?MarkerAnno(_) + ?SingleElemAnno(_, _)))

rules

  strip-unused-imports =
    with(used := <collect-all(?ClassType(_, <id>, _) + ?ClassType(<id>, _))>)
  ; CompilationUnit(id, filter(not(is-unused(\ x -> <getfirst(?x)> used \))), id)

  // 6.5. Determining the Meaning of a Name
  is-unused(used) = ?TypeName(<not(used)>)
  is-unused(used) = ?QualifiedTypeName(_, <not(used)>)

  // 7.5. Import Declarations
  is-unused(used) = ?SingleTypeImport(<is-unused(used)>)
  is-unused(used) = ?SingleStaticImport(_,_)
  is-unused(used) = ?StaticImportOnDemand(_)

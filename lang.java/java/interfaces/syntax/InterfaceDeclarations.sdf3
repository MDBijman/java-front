%%%
 %% Section 9.1: Interface Declarations
 %%%
module java/InterfaceDeclarations

imports

  java/Identifiers
  java/Modifiers
  java/Expressions
  java/ClassDeclarations
  java/MethodDeclarations
  java/ConstantDeclarations
  java/AbstractMethodDeclarations
  java/AnnotationTypes
  java/Annotations
  java/ReferenceTypes
  java/TypeVariables

sorts

  InterfaceDec InterfaceDecHead ExtendsInterfaces InterfaceMemberDec InterfaceMod

context-free syntax

  InterfaceDec = AnnoDec
  InterfaceDec.InterfaceDec = <<InterfaceDecHead> { <InterfaceMemberDec*> }>
  InterfaceDecHead.InterfaceDecHead = <<AnnoOrInterfaceMod*> interface <Id> <TypeParams?> <ExtendsInterfaces?>>
  ExtendsInterfaces.ExtendsInterfaces = <extends <{InterfaceType ","}+>>

context-free syntax

  InterfaceMemberDec = ConstantDec
  InterfaceMemberDec = AbstractMethodDec
  InterfaceMemberDec = ClassDec
  InterfaceMemberDec = InterfaceDec
  InterfaceMemberDec.Semicolon = <;>

context-free syntax

  InterfaceMod = Public
  InterfaceMod = Protected
  InterfaceMod = Private
  InterfaceMod = Abstract
  InterfaceMod = Static
  InterfaceMod = StrictFP
  
  AnnoOrInterfaceMod = Anno
  AnnoOrInterfaceMod = InterfaceMod

module pp

imports

  libstratego-gpp
  libspoofax/sdf/pp
  libspoofax/editor/refactoring/-
  runtime/task/-
  
  signatures/-
  pp/-
  
  types/languages/Java-1.5/types/types/references

rules

  editor-format:
    (node, _, ast, path, project-path) -> (filename, result)
    with
      ext      := <get-extension> path
    ; filename := <guarantee-extension(|$[pp.[ext]])> path
    ; result   := <pp-debug> node

rules

  pp-message-top = origin-track-forced((id, strip-annos; pp-message))
  pp-message = not(is-list); (is-string <+ pp-result <+ custom-pp-java-string <+ pp-java-string <+ (strip-annos; write-to-string))
  pp-message = map(pp-message); concat-strings

  pp-result:
    result@Result(_) -> msg
    where
      if <task-has-failed> result then
        msg := "fail"
      else
        msg := <task-get-results; map(pp-message); separate-by(|" or "); concat-strings> result
      end

  custom-pp-java-string:
    RefType(c, tp*) -> <pp-java-string> ClassOrInterfaceType(c, tp*)

rules

  pp-java-string =
    parenthesize-metaborg-java
    ; prettyprint-SDF-start-symbols
    ; !V([], <id>)
    ; box2text-string(|120)

  pp-partial-java-string =
    parenthesize-metaborg-java
    ; prettyprint-example
    ; !V([], <id>)
    ; box2text-string(|120)

  pp-debug :
    ast -> result
    with
       result := <pp-java-string> ast
    <+ <bottomup(try(not(is-string); not(is-list); not(pp-java-string); debug(!"cannot pp ")))> ast
    ;  result := ""

rules

  /**
  * Given an old AST and a new AST, pretty-prints the new AST while presevering
  * layout (e.g. whitespacing and comments) associated with the old AST.
  *
  * Example:
  *   new-text := <construct-textual-change> (old-ast, new-ast)
  */
  construct-textual-change = construct-textual-change(pp-partial-java-string, parenthesize, override-reconstruction, resugar)

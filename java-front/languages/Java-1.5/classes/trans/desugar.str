module languages/Java-1.5/classes/trans/desugar

imports

  signatures/-
  names

signature  // Access modifiers

  constructors

    Public    : Access
    Private   : Access
    Protected : Access
    Default   : Access

rules

  desugar-pick-access-modifier:
    m* -> a
    where
      a := <fetch-elem(?Public())> m* <+
      a := <fetch-elem(?Private())> m* <+
      a := <fetch-elem(?Protected())> m* <+
      a := Default()

signature // Context modifier

  constructors

    Static   : Context
    Instance : Context

rules

  desugar-pick-context-modifier:
    m* -> c
    where
      c := <fetch-elem(?Static())> m* <+
      c := Instance()

signature // Class

  constructors

    ClassDec : Access * Context * List(ClassMod) * Id * TypeParams * Super * Interfaces * ClassBody -> ClassDec
    ImplementsDec : Id * InterfaceType -> ImplementsDec

rules

  desugar:
    ClassDec(ClassDecHead(modifier*, name, type-param*, super-class, implements), body) ->
    ClassDec(access, context, modifier'*, name, type-param*, super-class', implements', body)
    with
      access       := <desugar-pick-access-modifier> modifier*
    ; context      := <desugar-pick-context-modifier> modifier*
    ; modifier'*   := <remove-all(?access <+ ?context)> modifier*
    ; super-class' := <desugar-super-class> super-class
    ; implements'  := <desugar-implements(|name)> implements

  desugar-super-class:
    SuperDec(super-class) -> SuperDec(super-class)

  desugar-super-class:
    // None() -> SuperDec(ClassType(TypeName(PackageOrTypeName(PackageOrTypeName("java"), "lang"), "Object"), None()))
    None() -> SuperDec(ClassType(TypeName("Object"), None()))

  desugar-implements(|c):
    ImplementsDec(interface-type*) -> <map(\it -> ImplementsDec(c, it)\)> interface-type*

  desugar-implements(|c):
    None() -> []

signature // Field

  constructors

    FieldDec : Access * Context * List(FieldMod) * Type * List(VarDec) -> FieldDec

rules

  desugar:
    FieldDec(modifier*, type, vardec*) -> <map(desugar-fielddec(|modifier*, type))> vardec*

  desugar-fielddec(|modifier*, type):
    var-dec -> FieldDec(access, context, modifier*, type, var-dec')
    with
      access       := <desugar-pick-access-modifier> modifier*
    ; context      := <desugar-pick-context-modifier> modifier*
    ; modifier'*   := <remove-all(?access <+ ?context)> modifier*
    ; var-dec'     := <try(desugar-fielddec-arrayinit(|type))> var-dec

  desugar-fielddec-arrayinit(|type):
    VarDec(v, ArrayInit(init*)) -> VarDec(v, NewArrayInit(ty, [Dim()], ArrayInit(init*)))
    where
      ArrayType(ty) := type

signature // Method

  constructors

    MethodDec : Access * Context * List(MethodMod) * TypeParams * Type * Id * MethodParams * Throws * MethodBody -> MethodDec

rules

  desugar:
    _ ->
    MethodDec(access, context, modifier'*, type-param*, type, name, param*, throws, body)
    where
      ?MethodDec(MethodDecHead(modifier*, type-param*, type, name, param*, throws), body)
    + ?MethodDec(DeprMethodDecHead(modifier*, type-param*, type, name, param*, _, throws), body)
    with
      access       := <desugar-pick-access-modifier> modifier*
    ; context      := <desugar-pick-context-modifier> modifier*
    ; modifier'*   := <remove-all(?access <+ ?context)> modifier*

signature // Constructor

  constructors

    ConstrDec : Access * List(ConstrMod) * TypeParams * Id * List(FormalParam) * Throws * ConstrBody -> ConstrDec
    ConstrBody : Statement -> ConstrBody

rules

  desugar:
    ConstrDec(ConstrDecHead(modifier*, type-param*, name, param*, throws), body) ->
    ConstrDec(access, modifier'*, type-param*, name, param*, throws, body')
    with
      access     := <desugar-pick-access-modifier> modifier*
    ; modifier'* := <remove-all(?access)> modifier*
    ; body'      := <desugar-constructor-body> body

  desugar-constructor-body:
    None() -> None()

  desugar-constructor-body:
    ConstrBody(None(), stmts) -> ConstrBody(stmts)

  desugar-constructor-body:
    ConstrBody(inv, stmts) -> ConstrBody([inv|stmts])
    where not(None() := inv)

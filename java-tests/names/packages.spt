module names-packages

language Java

start symbol Files

test fully qualified class name [[
	multifile
	
	package animals.meowable;

	class [[Cat]] {
		class Mouth {}
	}
	
	=====

	package barker;

	class Barker {
		animals.meowable.[[Cat]] cat;
	}
]] resolve #2 to #1

test half qualified class name [[
	multifile
	
	package animals.meowable;

	class Cat {
		class Mouth {}
	}
	
	=====

	package barker;

	class Barker {
		[[meowable.Cat]] cat;
	}
]] 3 errors // TODO: should be 2 errors, caused by filtering?



test fully qualified nested class name [[
	multifile
	
	package animals.meowable;

	class Cat {
		class [[Mouth]] {}
	}
	
	=====

	package barker;

	class Barker {
		animals.meowable.Cat.[[Mouth]] mouth;
	}
]] resolve #2 to #1

test half qualified nested class name 1 [[
	multifile
	
	package animals.meowable;

	class Cat {
		class Mouth {}
	}
	
	=====

	package barker;

	class Barker {
		[[meowable.Cat.Mouth]] mouth;
	}
]] 4 errors // TODO: should be 3 errors, caused by filtering?

test half qualified nested class name 2 [[
	multifile
	
	package animals.meowable;

	class Cat {
		class [[Mouth]] {}
	}
	
	=====

	package barker;
	
	import animals.meowable.Cat;

	class Barker {
		Cat.[[Mouth]] mouth;
	}
]] resolve #2 to #1



test unqualified class name without import [[
	multifile
	
	package animals.meowable;

	class Cat {
		class Mouth {}
	}
	
	=====

	package barker;

	class Barker {
		[[Cat]] cat;
	}
]] 2 errors // TODO: should be 1 error, caused by filtering?

test unqualified class name with class import [[
	multifile
	
	package animals.meowable;

	class [[Cat]] {
		class Mouth {}
	}
	
	=====

	package barker;
	
	import animals.meowable.Cat;

	class Barker {
		[[Cat]] cat;
	}
]] resolve #2 to #1

test unqualified class name with wildcard import [[
	multifile
	
	package animals.meowable;

	class [[Cat]] {
		class Mouth {}
	}
	
	=====

	package barker;
	
	import animals.meowable.*;

	class Barker {
		[[Cat]] cat;
	}
]] resolve #2 to #1



test unqualified nested class name with class import [[
	multifile
	
	package animals.meowable;

	class Cat {
		class [[Mouth]] {}
	}
	
	=====

	package barker;
	
	import animals.meowable.Cat.Mouth;

	class Barker {
		[[Mouth]] mouth;
	}
]] resolve #2 to #1

test unqualified nested class name with wildcard import [[
	multifile
	
	package animals.meowable;

	class Cat {
		class [[Mouth]] {}
	}
	
	=====

	package barker;
	
	import animals.meowable.*;

	class Barker {
		Cat.[[Mouth]] mouth;
	}
]] resolve #2 to #1



test package sharing [[
	multifile
	
	package animals.meowable;

	class [[Cat]] {
		class Mouth {}
	}
	
	=====
		
	package animals.meowable;
	
	class Barker {
		[[Cat]] cat;
	}
]] resolve #2 to #1

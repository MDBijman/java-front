module java/types/TypeVariable

imports

  signatures/java/types/TypeVariable-sig

  java/Object
  java/classes/ClassDeclarations
  java/interfaces/Annotations
  java/names/Main
  java/names/TypeNames
  java/types/Main
  java/types/ReferenceTypes


rules // 4.4. Type Variables

  /**
   * Type parameters are treated as local CLASS declarations.
   */

  typeParamOk : scope * TypeParameter
  typeParamListOk maps typeParamOk(*, list(*))

  typeParamOk(s, TypeParameter(annoList, id)) :-
  {s_var}
    annoListOk(s, annoList),

    new s_var,
    declareType(s, id, s_var, CLASS()),

    setSuperCls(s_var, objectOk(s)),

    true.

  typeParamOk(s, TypeParameterExtends(annoList, id, clsTypeList)) :-
  {s_var T Ts}
    annoListOk(s, annoList),

    new s_var,
    declareType(s, id, s_var, CLASS()),

    [T|Ts] == clsTypeListOk(s, clsTypeList),
    typeParamOk1(s_var, typeKind(T), T),
    addSuperIntfList(s_var, Ts).

        typeParamOk1 : scope * KIND * TYPE

        typeParamOk1(s_var, CLASS(), T) :-
          setSuperCls(s_var, T).

        typeParamOk1(s_var, INTF(), T) :-
          addSuperIntf(s_var, T).

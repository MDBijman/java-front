module java/types/ReferenceTypes

imports

  signatures/java/types/ReferenceTypes-sig

  java/interfaces/Annotations
  java/names/TypeNames
  java/names/PackageOrTypeNames
  java/types/Main
  java/types/ParameterizedTypes
  java/types/PrimitiveTypes


// 4.3. Reference Types and Values

signature

  constructors
    ARRAY     : TYPE -> TYPE
    CLASS     : scope -> TYPE
    ENUM      : scope -> TYPE
    INTF      : scope -> TYPE
  
rules 

  refTypeOk : scope * ReferenceType
  
  refTypeOk(s, ClassType2ReferenceType(clsType)) :-
    clsTypeOk(s, clsType),
    true.
    
  refTypeOk(s, ArrayType2ReferenceType(arrayType)) :-
    arrayTypeOk(s, arrayType),
    true.


  clsTypeOk : scope * ClassType
  clsTypeListOk maps clsTypeOk(*, list(*))
  
  clsTypeOk(s, ClassType(annoList, id, typeArgsOpt)) :-
  {typeDecl}
    annoListOk(s, annoList),
    typeDecl == singleTypeDecl(id, resolveLexicalTypeNames(s, id)),
    typeArgsOptOk(s, typeArgsOpt),
    true.

  clsTypeOk(s, ClassOrInterfaceTypeMember(clsType, annoList, id, typeArgsOpt)) :-
  {s_pkgOrCls typeDecl}
    s_pkgOrCls == pkgOrClsTypeOk(s, clsType),
    annoListOk(s, annoList),
    typeDecl == singleTypeDecl(id, resolveMemberTypeNames(s_pkgOrCls, id)),
    typeArgsOptOk(s, typeArgsOpt),
    true.


  pkgOrClsTypeOk : scope * ClassType -> scope

  pkgOrClsTypeOk(s, ClassType(annoList, id, typeArgsOpt)) = s_pkgOrCls :-
    annoListOk(s, annoList),
    s_pkgOrCls == pkgOrTypeDeclScope(resolveLexicalPkgOrTypeName(s, id)),
    typeArgsOptOk(s, typeArgsOpt),
    true.

  pkgOrClsTypeOk(s, ClassOrInterfaceTypeMember(clsType, annoList, id, typeArgsOpt)) = s_pkgOrCls :-
  {s_pkgOrCls'}
    s_pkgOrCls' == pkgOrClsTypeOk(s, clsType),
    annoListOk(s, annoList),
    s_pkgOrCls == pkgOrTypeDeclScope(resolveMemberPkgOrTypeName(s, id)),
    typeArgsOptOk(s, typeArgsOpt),
    true.


  typeVarOk : scope * TypeVariable
  
  typeVarOk(s, TypeVar(annoList, id)) :-
    annoListOk(s, annoList),
    true.


  arrayTypeOk : scope * ArrayType
  
  arrayTypeOk(s, ArrayTypePrimitive(primType, annDims)) :-
    primTypeOk(s, primType),
    annDimsOk(s, annDims),
    true.

  arrayTypeOk(s, ArrayTypeClassType(clsType, annDims)) :-
    clsTypeOk(s, clsType),
    annDimsOk(s, annDims),
    true.


  annDimsOk : scope * AnnotatedDims
  
  annDimsOk(s, AnnotatedDims2AnnotatedDims(annDimList)) :-
    annDimListOk(s, annDimList),
    true.


  annDimsEmptyOk : scope * AnnotatedDimsEmpty
  
  annDimsEmptyOk(s, AnnotatedDims2AnnotatedDimsEmpty(annDimList)) :-
    annDimListOk(s, annDimList),
    true.


  annDimOk : scope * AnnotatedDim
  annDimListOk maps annDimOk(*, list(*))

  annDimOk(s, AnnotateDim(annoList)) :-
    annoListOk(s, annoList),
    true.


rules

  typeScope : TYPE -> scope
  
  typeScope(CLASS(s_cls)) = s_cls.

  typeScope(INTF(s_intf)) = s_intf.

  typeScope(ENUM(s_enum)) = s_enum.


module java/classes/ClassDeclarations

imports

  signatures/java/classes/ClassDeclarations-sig

  java/classes/Main
  java/classes/ConstructorDeclarations
  java/classes/EnumDeclarations
  java/classes/FieldDeclarations
  java/classes/InstanceInitializers
  java/classes/MethodDeclarations
  java/classes/StaticInitializers
  java/interfaces/Annotations
  java/interfaces/InterfaceDeclarations
  java/names/Main
  java/names/TypeNames
  java/types/Main
  java/types/ReferenceTypes
  java/types/TypeVariable


rules // 8.1. Class Declarations

  clsDeclOk : scope * ClassDeclaration
  
  clsDeclOk(s, NormalClassDeclaration2ClassDeclaration(normalClsDecl)) :-
    normalClsDeclOk(s, normalClsDecl),
    true.

  clsDeclOk(s, EnumDeclaration2ClassDeclaration(enumDecl)) :-
    enumDeclOk(enumDecl),
    true.


  normalClsDeclOk : scope * NormalClassDeclaration
  
  normalClsDeclOk(s, ClassDeclaration(clsModList, id, typeParamsOpt, superClsOpt, superIntfsOpt, clsBodyDeclList)) :-
  {s_cls}
    clsModListOk(clsModList),

    new s_cls,
    declareType(s, id, CLASS(s_cls)),
    // FIXME noDuplicateType
    // FIXME noEnclosingTypeSameName
    // FIXME noClassCycle
    
    typeParamsOptOk(typeParamsOpt),

    superClsOptOk(s, superClsOpt),
    superIntfsOptOk(s, superIntfsOpt),

    // FIXME declare self (?)
    s_cls -LEX-> s,

    // FIXME declare `this`

    clsBodyDeclListOk(clsBodyDeclList),
    true.


  clsModOk : ClassModifier
  clsModListOk maps clsModOk(list(*))
  
  clsModOk(Annotation2ClassModifier(anno)) :-
    annoOk(anno),
    true.
  clsModOk(Public2ClassModifier(_)) :- true.
  clsModOk(Protected2ClassModifier(_)) :- true.
  clsModOk(Private2ClassModifier(_)) :- true.
  clsModOk(Abstract2ClassModifier(_)) :- true.
  clsModOk(Static2ClassModifier(_)) :- true.
  clsModOk(Final2ClassModifier(_)) :- true.
  clsModOk(Strictfp2ClassModifier(_)) :- true.


  typeParamsOk : TypeParameters
  typeParamsOptOk maps typeParamsOk(list(*))
  
  typeParamsOk(TypeParameters(typeParamList)) :-
    typeParamListOk(typeParamList),
    true.


  superClsOk : scope * SuperClass
  superClsOptOk maps superClsOk(*, list(*))
  
  superClsOk(s, SuperClass(clsType)) :-
    clsTypeOk(s, clsType),
    true.


  superIntfsOk : scope * SuperInterfaces
  superIntfsOptOk maps superIntfsOk(*, list(*))
  
  superIntfsOk(s, SuperInterface(clsTypeList)) :-
    clsTypeListOk(s, clsTypeList),
    true.


  clsBodyDeclOk : ClassBodyDeclaration
  clsBodyDeclListOk maps clsBodyDeclOk(list(*))
  
  clsBodyDeclOk(ClassMemberDeclaration2ClassBodyDeclaration(clsMemDecl)) :-
    clsMemDeclOk(clsMemDecl),
    true.

  clsBodyDeclOk(InstanceInitializer2ClassBodyDeclaration(instInit)) :-
    instInitOk(instInit),
    true.

  clsBodyDeclOk(StaticInitializer2ClassBodyDeclaration(staticInit)) :-
    staticInitOk(staticInit),
    true.

  clsBodyDeclOk(ConstructorDeclaration2ClassBodyDeclaration(ctorDecl)) :-
    ctorDeclOk(ctorDecl),
    true.


  clsMemDeclOk : ClassMemberDeclaration
  
  clsMemDeclOk(FieldDeclaration2ClassMemberDeclaration(fldDecl)) :-
    fldDeclOk(fldDecl),
    true.
  
  clsMemDeclOk(MethodDeclaration2ClassMemberDeclaration(mthdDecl)) :-
    mthdDeclOk(mthdDecl),
    true.
  
  clsMemDeclOk(ClassDeclaration2ClassMemberDeclaration(clsDecl)) :-
    clsDeclOk(_, clsDecl),
    true.

  clsMemDeclOk(InterfaceDeclaration2ClassMemberDeclaration(intfDecl)) :-
    intfDeclOk(_, intfDecl),
    true.

  clsMemDeclOk(SemiColon2ClassMemberDeclaration(semiColon)) :-
    true.



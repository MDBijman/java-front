module java/classes/ClassDeclarations

imports

  signatures/java/classes/ClassDeclarations-sig

  java/Object
  java/classes/Main
  java/classes/ConstructorDeclarations
  java/classes/EnumDeclarations
  java/classes/FieldDeclarations
  java/classes/InstanceInitializers
  java/classes/MethodDeclarations
  java/classes/StaticInitializers
  java/interfaces/Annotations
  java/interfaces/InterfaceDeclarations
  java/names/Main
  java/names/TypeNames
  java/types/Main
  java/types/ReferenceTypes
  java/types/TypeVariable


rules // 8.1. Class Declarations

  clsDeclOk : scope * ClassDeclaration
  
  clsDeclOk(s, NormalClassDeclaration2ClassDeclaration(normalClsDecl)) :-
    normalClsDeclOk(s, normalClsDecl),
    true.

  clsDeclOk(s, EnumDeclaration2ClassDeclaration(enumDecl)) :-
    enumDeclOk(s, enumDecl),
    true.


  normalClsDeclOk : scope * NormalClassDeclaration
  
  normalClsDeclOk(s, ClassDeclaration(clsModList, id, typeParamsOpt, superClsOpt, superIntfsOpt, clsBodyDeclList)) :-
  {s_cls}
    clsModListOk(s, clsModList),

    new s_cls,
    declareType(s, id, s_cls, CLASS()),
    // FIXME noDuplicateType
    // FIXME noEnclosingTypeSameName
    // FIXME noClassCycle
    
    typeParamsOptOk(s, typeParamsOpt, s_cls),

    superClsOptOk(s, superClsOpt, s_cls),
    superIntfsOptOk(s, superIntfsOpt, s_cls),

    // FIXME declare self (?)
    s_cls -LEX-> s,

    clsBodyDeclListOk(s_cls, clsBodyDeclList),
    true.


  clsModOk : scope * ClassModifier
  clsModListOk maps clsModOk(*, list(*))
  
  clsModOk(s, Annotation2ClassModifier(anno)) :-
    annoOk(s, anno),
    true.
  clsModOk(s, Public2ClassModifier(_)) :- true.
  clsModOk(s, Protected2ClassModifier(_)) :- true.
  clsModOk(s, Private2ClassModifier(_)) :- true.
  clsModOk(s, Abstract2ClassModifier(_)) :- true.
  clsModOk(s, Static2ClassModifier(_)) :- true.
  clsModOk(s, Final2ClassModifier(_)) :- true.
  clsModOk(s, Strictfp2ClassModifier(_)) :- true.


  typeParamsOk : scope * TypeParameters * scope
  typeParamsOptOk maps typeParamsOk(*, list(*), *)
  
  typeParamsOk(s, TypeParameters(typeParamList), s_cls) :-
    typeParamListOk(s, typeParamList, s_cls),
    true.


  superClsOptOk : scope * list(SuperClass) * scope
  
  superClsOptOk(s, [], s_cls) :-
  {T}
    T == objectOk(s),
    setSuperCls(s_cls, T).

  superClsOptOk(s, [SuperClass(clsType)], s_cls) :-
  {T}
    T == clsTypeOk(s, clsType),
    setSuperCls(s_cls, T).

  setSuperCls : scope * TYPE

  setSuperCls(s_cls, T) :-
    isClsType(T),
    s_cls -EXT-> typeScope(T).


  superIntfsOk : scope * SuperInterfaces * scope
  superIntfsOptOk maps superIntfsOk(*, list(*), *)
  
  superIntfsOk(s, SuperInterface(clsTypeList), s_cls) :-
  {Ts}
    Ts == clsTypeListOk(s, clsTypeList),
    addSuperIntfList(s_cls, Ts).

  addSuperIntf : scope * TYPE
  addSuperIntfList maps addSuperIntf(*, list(*))
  
  addSuperIntf(s_cls, T) :-
    isIntfType(T),
    s_cls -IMPL-> typeScope(T).



  clsBodyDeclOk : scope * ClassBodyDeclaration
  clsBodyDeclListOk maps clsBodyDeclOk(*, list(*))
  
  clsBodyDeclOk(s, ClassMemberDeclaration2ClassBodyDeclaration(clsMemDecl)) :-
    clsMemDeclOk(s, clsMemDecl),
    true.

  clsBodyDeclOk(s, InstanceInitializer2ClassBodyDeclaration(instInit)) :-
    instInitOk(instInit),
    true.

  clsBodyDeclOk(s, StaticInitializer2ClassBodyDeclaration(staticInit)) :-
    staticInitOk(staticInit),
    true.

  clsBodyDeclOk(s, ConstructorDeclaration2ClassBodyDeclaration(ctorDecl)) :-
    ctorDeclOk(s, ctorDecl),
    true.


  clsMemDeclOk : scope * ClassMemberDeclaration
  
  clsMemDeclOk(s, FieldDeclaration2ClassMemberDeclaration(fldDecl)) :-
    fldDeclOk(s, fldDecl),
    true.
  
  clsMemDeclOk(s, MethodDeclaration2ClassMemberDeclaration(mthdDecl)) :-
    mthdDeclOk(s, mthdDecl),
    true.
  
  clsMemDeclOk(s, ClassDeclaration2ClassMemberDeclaration(clsDecl)) :-
    clsDeclOk(s, clsDecl),
    true.

  clsMemDeclOk(s, InterfaceDeclaration2ClassMemberDeclaration(intfDecl)) :-
    intfDeclOk(s, intfDecl),
    true.

  clsMemDeclOk(s, SemiColon2ClassMemberDeclaration(semiColon)) :-
    true.



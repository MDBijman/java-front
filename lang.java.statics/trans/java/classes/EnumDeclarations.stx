module java/classes/EnumDeclarations

imports

  signatures/java/classes/EnumDeclarations-sig

  java/classes/ClassDeclarations
  java/classes/Main
  java/expressions/Main
  java/interfaces/Annotations
  java/names/Main
  java/names/TypeNames


rules // 8.9. Enum Types

  enumDeclOk : scope * EnumDeclaration * scope

  enumDeclOk(s, EnumDeclComma(clsModList, id, superIntfsOpt, enumConstList, enumBodyDeclsOpt), s_def) :-
  {s_enum}
    clsModListOk(s, clsModList),

    new s_enum,
    declareType(s_def, id, s_enum, CLASS()),

    superIntfsOptOk(s_enum, superIntfsOpt, s_enum),

    s_enum -LEX-> s,

    enumConstListOk(s_enum, enumConstList),
    enumBodyDeclsOptOk(s_enum, enumBodyDeclsOpt),
    true.

  enumDeclOk(s, EnumDecl(clsModList, id, superIntfsOpt, enumConstList, enumBodyDeclsOpt), s_def) :-
  {s_enum}
    clsModListOk(s, clsModList),

    new s_enum,
    declareType(s_def, id, s_enum, CLASS()),

    superIntfsOptOk(s, superIntfsOpt, s_enum),

    s_enum -LEX-> s,

    enumConstListOk(s_enum, enumConstList),
    enumBodyDeclsOptOk(s_enum, enumBodyDeclsOpt),
    true.


  enumConstOk : scope * EnumConstant
  enumConstListOk maps enumConstOk(*, list(*))

  enumConstOk(s, EnumConst(annoList, id, enumConstArgsOpt, clsBodyDeclList)) :-
  {s_const}
    annoListOk(s, annoList),

    new s_const, s_const -LEX-> s,

    enumConstArgsOptOk(s, enumConstArgsOpt),
    clsBodyDeclListOk(s_const, clsBodyDeclList),
    true.

  enumConstOk(s, EnumConstNoBody(annoList, id, enumConstArgsOpt)) :-
  {s_const}
    annoListOk(s, annoList),

    new s_const, s_const -LEX-> s,

    enumConstArgsOptOk(s, enumConstArgsOpt),
    true.


  enumConstArgsOk : scope * EnumConstantArgs
  enumConstArgsOptOk maps enumConstArgsOk(*, list(*))

  enumConstArgsOk(s, EnumConstArgs(exprList)) :-
    _ == exprListOk(s, exprList),
    true.


  enumBodyDeclsOk : scope * EnumBodyDeclarations
  enumBodyDeclsOptOk maps enumBodyDeclsOk(*, list(*))

  enumBodyDeclsOk(s, EnumBodyDecs(clsBodyDeclList)) :-
    clsBodyDeclListOk(s, clsBodyDeclList),
    true.


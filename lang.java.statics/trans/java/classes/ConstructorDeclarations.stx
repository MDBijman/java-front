module java/classes/ConstructorDeclarations

imports

  signatures/java/classes/ConstructorDeclarations-sig

  java/classes/ClassDeclarations
  java/classes/Main
  java/classes/MethodDeclarations
  java/expressions/Main
  java/interfaces/Annotations
  java/names/Main
  java/statements/Blocks
  java/types/ParameterizedTypes
  java/types/TypeVariable


rules // 8.8. Constructor Declarations

  ctorDeclOk : scope * ConstructorDeclaration

  ctorDeclOk(s, ConstrDecl(ctorModList, typeParamsOpt, id, formalParams, throwsOpt, ctorInvOpt, blkStmtList)) :-
  {s_ctor s_typeParams}
    ctorModListOk(s, ctorModList),

    new s_ctor,
    
    s_typeParams == typeParamsOptOk(s, typeParamsOpt),
    s_ctor -LEX-> s_typeParams,

    formalParamsOk(s_typeParams, formalParams, s_ctor),
    throwsOptOk(s_typeParams, throwsOpt),
    ctorInvOptOk(s_ctor, ctorInvOpt),
    blkStmtListOk(s_ctor, blkStmtList),
    true.


  ctorModOk : scope * ConstructorModifier
  ctorModListOk maps ctorModOk(*, list(*))

  ctorModOk(s, Annotation2ConstructorModifier(anno)) :-
    annoOk(s, anno),
    true.
  ctorModOk(s, Public2ConstructorModifier(_)) :- true.
  ctorModOk(s, Private2ConstructorModifier(_)) :- true.
  ctorModOk(s, Protected2ConstructorModifier(_)) :- true.


  ctorInvOk : scope * ConstructorInvocation
  ctorInvOptOk maps ctorInvOk(*, list(*))

  ctorInvOk(s, AltConstrInv(typeArgsOpt, exprList)) :-
    _ == typeArgsOptOk(s, typeArgsOpt),
    _ == exprListOk(s, exprList),
    true.

  ctorInvOk(s, SuperConstrInv(typeArgsOpt, exprList)) :-
    _ == typeArgsOptOk(s, typeArgsOpt),
    _ == exprListOk(s, exprList),
    true.

  ctorInvOk(s, ExprNameConstrInv(expr, typeArgsOpt, exprList)) :-
    _ == exprOk(s, expr),
    _ == typeArgsOptOk(s, typeArgsOpt),
    _ == exprListOk(s, exprList),
    true.



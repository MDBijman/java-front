module java/interfaces/AnnotationTypes

imports

  signatures/java/interfaces/AnnotationTypes-sig

  java/classes/ClassDeclarations
  java/classes/FieldDeclarations
  java/interfaces/Annotations
  java/interfaces/InterfaceDeclarations
  java/interfaces/ConstantDeclarations
  java/interfaces/Main
  java/names/Main
  java/names/TypeNames
  java/types/ReferenceTypes


rules // 9.6. Annotation Types

  annoTypeDeclOk : scope * AnnotationTypeDeclaration

  annoTypeDeclOk(s, AnnoDec(intfModList, id, annoTypeMemDeclList)) :-
  {s_anno}
    intfModListOk(s, intfModList),

    new s_anno,
    declareType(s, id, s_anno, INTF()),

    s_anno -LEX-> s,

    // FIXME Object as supertype?

    annoTypeMemDeclListOk(s_anno, annoTypeMemDeclList),
    true.


  annoTypeMemDeclOk : scope * AnnotationTypeMemberDeclaration
  annoTypeMemDeclListOk maps annoTypeMemDeclOk(*, list(*))

  annoTypeMemDeclOk(s, AnnotationTypeElementDeclaration2AnnotationTypeMemberDeclaration(annoTypeElemDecl)) :-
    annoTypeElemDeclOk(s, annoTypeElemDecl),
    true.

  annoTypeMemDeclOk(s, ConstantDeclaration2AnnotationTypeMemberDeclaration(constDecl)) :-
    constDeclOk(s, constDecl),
    true.

  annoTypeMemDeclOk(s, ClassDeclaration2AnnotationTypeMemberDeclaration(clsDecl)) :-
    clsDeclOk(s, clsDecl),
    true.

  annoTypeMemDeclOk(s, InterfaceDeclaration2AnnotationTypeMemberDeclaration(intfDecl)) :-
    intfDeclOk(s, intfDecl),
    true.

  annoTypeMemDeclOk(s, SemiColon2AnnotationTypeMemberDeclaration(_)) :-
    true.


  annoTypeElemDeclOk : scope * AnnotationTypeElementDeclaration

  annoTypeElemDeclOk(s, AnnotationTypeElementDecl(annoTypeElemModList, unannType, id, annDimsEmpty, defaultValOpt)) :-
    annoTypeElemModListOk(s, annoTypeElemModList),
    _ == unannTypeOk(s, unannType),
    annDimsEmptyOk(s, annDimsEmpty),
    defaultValOptOk(s, defaultValOpt),
    true.


  annoTypeElemModOk : scope * AnnotationTypeElementModifier
  annoTypeElemModListOk maps annoTypeElemModOk(*, list(*))

  annoTypeElemModOk(s, Annotation2AnnotationTypeElementModifier(anno)) :-
    annoOk(s, anno),
    true.
  annoTypeElemModOk(s, Public2AnnotationTypeElementModifier(_)) :- true.
  annoTypeElemModOk(s, Abstract2AnnotationTypeElementModifier(_)) :- true.


  defaultValOk : scope * DefaultValue
  defaultValOptOk maps defaultValOk(*, list(*))

  defaultValOk(s, DefaultVal(elemVal)) :-
    elemValOk(s, elemVal),
    true.



module java/interfaces/InterfaceDeclarations

imports

  signatures/java/interfaces/InterfaceDeclarations-sig

  java/classes/ClassDeclarations
  java/interfaces/Annotations
  java/interfaces/AnnotationTypes
  java/interfaces/ConstantDeclarations
  java/interfaces/Main
  java/interfaces/InterfaceMethodDeclarations
  java/names/Main
  java/names/TypeNames
  java/types/ReferenceTypes


rules // 9.1. Interface Declarations

  intfDeclOk : scope * InterfaceDeclaration
  
  intfDeclOk(s, NormalInterface(intfModList, id, typeParamsOpt, extendsIntfsOpt, intfMemDeclList)) :-
  {s_intf}
    intfModListOk(intfModList),
    
    new s_intf,
    declareType(s, id, INTF(s_intf)),
    // FIXME noDuplicateType
    // FIXME noEnclosingTypeSameName
    // FIXME noExtendsCycle
    
    typeParamsOptOk(typeParamsOpt),

    // FIXME declare self (?)
    s_intf -LEX-> s,

    extendsIntfsOptOk(s, extendsIntfsOpt),
    
    // FIXME declare `this`
    
    intfMemDeclListOk(intfMemDeclList),
    true.

  intfDeclOk(s, AnnotationTypeDeclaration2InterfaceDeclaration(annoTypeDecl)) :-
    annoTypeDeclOk(annoTypeDecl),
    true.
    
    
  intfModOk : InterfaceModifier
  intfModListOk maps intfModOk(list(*))
  
  intfModOk(Annotation2InterfaceModifier(anno)) :-
    annoOk(anno),
    true.
  intfModOk(Public2InterfaceModifier(_)) :- true.
  intfModOk(Protected2InterfaceModifier(_)) :- true.
  intfModOk(Private2InterfaceModifier(_)) :- true.
  intfModOk(Abstract2InterfaceModifier(_)) :- true.
  intfModOk(Static2InterfaceModifier(_)) :- true.
  intfModOk(Strictfp2InterfaceModifier(_)) :- true.


  extendsIntfsOk : scope * ExtendsInterfaces
  extendsIntfsOptOk maps extendsIntfsOk(*, list(*))
  
  extendsIntfsOk(s, ExtendsInterfaces(clsTypeList)) :-
    clsTypeListOk(s, clsTypeList),
    true.
    
    
  intfMemDeclOk : InterfaceMemberDeclaration
  intfMemDeclListOk maps intfMemDeclOk(list(*))
  
  intfMemDeclOk(ConstantDeclaration2InterfaceMemberDeclaration(constDecl)) :-
    constDeclOk(constDecl),
    true.

  intfMemDeclOk(InterfaceMethodDeclaration2InterfaceMemberDeclaration(intfMthdDecl)) :-
    intfMthdDeclOk(intfMthdDecl),
    true.

  intfMemDeclOk(ClassDeclaration2InterfaceMemberDeclaration(clsDecl)) :-
    clsDeclOk(_, clsDecl),
    true.

  intfMemDeclOk(InterfaceDeclaration2InterfaceMemberDeclaration(intfDecl)) :-
    intfDeclOk(_, intfDecl),
    true.

  intfMemDeclOk(SemiColon2InterfaceMemberDeclaration(_)) :-
    true.



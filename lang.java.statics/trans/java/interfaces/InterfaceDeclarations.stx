module java/interfaces/InterfaceDeclarations

imports

  signatures/java/interfaces/InterfaceDeclarations-sig

  java/classes/ClassDeclarations
  java/interfaces/Annotations
  java/interfaces/AnnotationTypes
  java/interfaces/ConstantDeclarations
  java/interfaces/Main
  java/interfaces/InterfaceMethodDeclarations
  java/names/Main
  java/names/TypeNames
  java/types/ReferenceTypes


rules // 9.1. Interface Declarations

  intfDeclOk : scope * InterfaceDeclaration
  
  intfDeclOk(s, NormalInterface(intfModList, id, typeParamsOpt, extendsIntfsOpt, intfMemDeclList)) :-
  {s_intf}
    intfModListOk(s, intfModList),
    
    new s_intf,
    declareType(s, id, INTF(s_intf)),
    // FIXME noDuplicateType
    // FIXME noEnclosingTypeSameName
    // FIXME noExtendsCycle
    
    typeParamsOptOk(s, typeParamsOpt, s_intf),

    // FIXME declare self (?)
    s_intf -LEX-> s,

    extendsIntfsOptOk(s, extendsIntfsOpt),
    
    // FIXME declare `this`
    
    intfMemDeclListOk(s_intf, intfMemDeclList),
    true.

  intfDeclOk(s, AnnotationTypeDeclaration2InterfaceDeclaration(annoTypeDecl)) :-
    annoTypeDeclOk(s, annoTypeDecl),
    true.
    
    
  intfModOk : scope * InterfaceModifier
  intfModListOk maps intfModOk(*, list(*))
  
  intfModOk(s, Annotation2InterfaceModifier(anno)) :-
    annoOk(s, anno),
    true.
  intfModOk(s, Public2InterfaceModifier(_)) :- true.
  intfModOk(s, Protected2InterfaceModifier(_)) :- true.
  intfModOk(s, Private2InterfaceModifier(_)) :- true.
  intfModOk(s, Abstract2InterfaceModifier(_)) :- true.
  intfModOk(s, Static2InterfaceModifier(_)) :- true.
  intfModOk(s, Strictfp2InterfaceModifier(_)) :- true.


  extendsIntfsOk : scope * ExtendsInterfaces
  extendsIntfsOptOk maps extendsIntfsOk(*, list(*))
  
  extendsIntfsOk(s, ExtendsInterfaces(clsTypeList)) :-
    clsTypeListOk(s, clsTypeList),
    true.
    
    
  intfMemDeclOk : scope * InterfaceMemberDeclaration
  intfMemDeclListOk maps intfMemDeclOk(*, list(*))
  
  intfMemDeclOk(s, ConstantDeclaration2InterfaceMemberDeclaration(constDecl)) :-
    constDeclOk(s, constDecl),
    true.

  intfMemDeclOk(s, InterfaceMethodDeclaration2InterfaceMemberDeclaration(intfMthdDecl)) :-
    intfMthdDeclOk(s, intfMthdDecl),
    true.

  intfMemDeclOk(s, ClassDeclaration2InterfaceMemberDeclaration(clsDecl)) :-
    clsDeclOk(s, clsDecl),
    true.

  intfMemDeclOk(s, InterfaceDeclaration2InterfaceMemberDeclaration(intfDecl)) :-
    intfDeclOk(s, intfDecl),
    true.

  intfMemDeclOk(s, SemiColon2InterfaceMemberDeclaration(_)) :-
    true.



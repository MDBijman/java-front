module analysis

imports

  libspoofax/core/language
  libspoofax/sdf/pp
  libspoofax/term/origin

  statix/api
  statixruntime

  java/injections

  signatures/-
  signatures/java/packages/-
  pp/-

rules // Analysis

  editor-analyze = stx-editor-analyze(pre-analyze,post-analyze|"statics", "projectOk", "fileOk")

  pre-analyze = (import-java-lang) ; java-explicate-injections

  post-analyze = java-implicate-injections

  import-java-lang:
      CompilationUnit(pkgDecl, importDeclList, typeDeclList) -> CompilationUnit(pkgDecl, importDeclList', typeDeclList)
    with javaLangImport  := <origin-track-forced(!TypeImportOnDemand(PackageOrTypeName(PackageOrTypeName(Id("java")),Id("lang"))))> pkgDecl
       ; importDeclList' := [javaLangImport|importDeclList]

rules // Editor services
  
  editor-resolve = stx-editor-resolve

  editor-hover = stx-editor-hover

rules // Debugging

  debug-show-pre-analysis:
    (_, _, ast, path, _) -> (filename, result)
    with
      filename := <guarantee-extension(|"pre-analysis.aterm")> path;
      result   := (filename, <pre-analyze> ast)

  debug-show-analyzed:
    (_, _, ast, path, _) -> (filename, result)
    with
      filename := <guarantee-extension(|"analyzed.aterm")> path;
      result   := ast

